IF
	GlobalGT("AC_Waukeen_Free","GLOBAL",0)
	Global("PC_Return","AC#W04",0)
THEN
	RESPONSE #100
		BanterBlockTime(600)
		DayNight(NOON)
		Wait(1)
		SetGlobal("PC_Return","AC#W04",1)
		ActionOverride("ACHALAN1",StartDialogueNoSet(Player1))
END

IF
	Global("PC_Return","AC#W04",2)
THEN
	RESPONSE #100
		ClearAllActions()
		StartCutSceneMode()
		ActionOverride("actharun",JumpToPoint([1175.1421]))
		Wait(1)
		SetGlobal("PC_Return","AC#W04",3)
		ActionOverride("actharun",StartDialogueNoSet([PC]))
END

/*
IF
	Global("PC_Return","AC#W04",4)
THEN
	RESPONSE #100
		SetGlobal("PC_Return","AC#W04",5)
		CreateCreature("AC#W04P1",[1264.1528],5)  // Barasta Cleeith
		ActionOverride("AC#W04P1",Dialogue("actharun"))  // Tharundar Olehm
END
*/

IF
	Global("AC_Accepted","GLOBAL",2)
THEN
	RESPONSE #100
		ClearAllActions()
		StartCutSceneMode()
		FadeToColor([20.0],0)
		Wait(1)
		MoveViewPoint([1235.816],INSTANT)
		ActionOverride(Player1,JumpToPoint([1069.788]))
		ActionOverride(Player2,JumpToPoint([1069.788]))
		ActionOverride(Player3,JumpToPoint([1069.788]))
		ActionOverride(Player4,JumpToPoint([1069.788]))
		ActionOverride(Player5,JumpToPoint([1069.788]))
		ActionOverride(Player6,JumpToPoint([1069.788]))
		ActionOverride("ACKYRIAN",JumpToPoint([1131.820]))
		ActionOverride("actharun",JumpToPoint([1235.816]))
		Wait(1)
		FadeFromColor([20.0],0)
		Wait(1)
		SetGlobal("AC_Accepted","GLOBAL",3)
		ActionOverride("ACKYRIAN",StartDialogueNoSet([PC]))
END

IF
	Global("AC_VisitHalanna","GLOBAL",1)
THEN
	RESPONSE #100
		ClearAllActions()
		ActionOverride("actharun",MoveToPointNoInterrupt([1175.1421]))
		Wait(2)
		SetGlobal("AC_VisitHalanna","GLOBAL",2)
END

IF
	Global("AC_Kyriani","GLOBAL",1)
THEN
	RESPONSE #100
		ClearAllActions()
		StartCutSceneMode()
		Wait(1)
		OpenDoor("Secret01")
		OpenDoor("Secret02")
		OpenDoor("Secret03")
		OpenDoor("Secret04")
		CreateCreature("ACKYRIAN",[997.1282],0)  // Kyriani
		SetGlobal("AC_Kyriani","GLOBAL",2)
		ActionOverride("ACKYRIAN",MoveToPoint([1001.1411]))
		Wait(1)
		ActionOverride("ACKYRIAN",Dialogue(Player1))
END

IF
	Global("AC_VisitHalanna","GLOBAL",3)
THEN
	RESPONSE #100
		Wait(2)
		SetGlobal("AC_VisitHalanna","GLOBAL",4)
		ActionOverride("actharun",StartDialogueNoSet(Player1))
END

IF
	GlobalGT("AC#WCelebration","GLOBAL",1)
	Global("ChangeHalanna","AC#W04",0)
	Exists("ACHALAN1")  // Halanna
THEN
	RESPONSE #100
		SetGlobal("ChangeHalanna","AC#W04",1)
		ActionOverride("ACHALAN1",DestroySelf())
END

IF
	GlobalGT("AC#WCelebration","GLOBAL",1)
	Exists("AC#W04P1")  // Barasta Cleeith
	Global("ChangeBarasta","AC#W04",0)
THEN
	RESPONSE #100
		SetGlobal("ChangeBarasta","AC#W04",1)
		ActionOverride("AC#W04P1",DestroySelf())
END

IF
	GlobalGT("AC#WCelebration","GLOBAL",1)
	Exists("ACTHARUN")  // Tharundar Olehm
	Global("ChangeTharundar","AC#W04",0)
THEN
	RESPONSE #100
		SetGlobal("ChangeTharundar","AC#W04",1)
		ActionOverride("ACTHARUN",DestroySelf())
		CreateCreature("AC#W04T2",[1354.1047],8)  // Tharundar Olehm
END

